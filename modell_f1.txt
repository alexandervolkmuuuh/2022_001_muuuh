/*
------------------------------------------------------------Customer------------------------------------------------------------
*/
Customer:
LOAD
Kundenart as %Kundenart_F1,
%DatumNum as %DatumNum_NAV,
Kundentyp as %Kundentyp,
    *
FROM [lib://$(v_bereich):DataFiles/$(v_start_datei)_02_Trans_04_F1_Customer.qvd]
(qvd);




/*
------------------------------------------------------------Kundentyp------------------------------------------------------------
*/
Kundentyp:
LOAD
    "Kundentyp-Code" as %Kundentyp,
    "Customer Type"
FROM [lib://$(v_bereich):DataFiles/$(v_start_datei)_01_Ext_04_F1_META_Kundentyp.qvd]
(qvd);


/*
------------------------------------------------------------Data_Protection------------------------------------------------------------
*/
Data_Protection:
LOAD
*
FROM [lib://$(v_bereich):DataFiles/$(v_start_datei)_02_Trans_04_F1_Data_Protection.qvd]
(qvd);


/*
------------------------------------------------------------Fehler Mail------------------------------------------------------------
*/
Mail_Fehler:
LOAD
	%Kundennummer_Standort,
    No_,
    "E-Mail",
    Flag_EMail_empty,
    Flag_EMail_Länge,
    Flag_EMail_@_valid,
    Flag_EMail_valid,
    Flag_EMail_Lokalteil_valid,
    Flag_EMail_Domainteil_valid,    
    Flag_Email_vollständig
FROM [lib://$(v_bereich):DataFiles/$(v_start_datei)_02_Trans_04_F1_Mail_Fehler.qvd]
(qvd);


/*
------------------------------------------------------------Fehler Telefon------------------------------------------------------------
*/
Telefon_Fehler:

LOAD
    %Kundennummer_Standort,
    "Phone No_",
    Flag_PhoneNo_empty,
    Flag_PhoneNo_valid,
    Flag_PhoneNo_Länge,
    Flag_PhoneNo_MaximalEinPlus,
    Flag_Phone_vollständig
FROM [lib://$(v_bereich):DataFiles/$(v_start_datei)_02_Trans_04_F1_Telefon_Fehler.qvd]
(qvd);


/*
------------------------------------------------------------META_Filiale------------------------------------------------------------
*/
Standorte:
LOAD
    Filialnummer as %Standorte,
    Filialbezeichnung as Name
    
FROM [lib://$(v_bereich):DataFiles/$(v_start_datei)_01_Ext_04_F1_META_Filiale.qvd]
(qvd);


/*
------------------------------------------------------------META_Kundenart------------------------------------------------------------
*/
Kundenart_F1:
LOAD
    "Kundenart-Code" as %Kundenart_F1,
    "Kundenart-Kontrolle",
    "Kundenart-Kurzbez.",
    "Kundenart-Langbez."
FROM [lib://$(v_bereich):DataFiles/$(v_start_datei)_01_Ext_04_F1_META_Kundenart.qvd]
(qvd);

/*
----------------------------------------------Vollständigkeit DSE---------------------------------------------------------------
*/

Unqualify *; 

Vollständigkeit_DSE_temp:
Load distinct 
	%Kundennummer_Standort,
    
     'Kontakt vollständig' as Ausprägung_Vollständigkeit
    
resident Customer where Customer.Flag_Kontakt_vollständig = 1;



Concatenate(Vollständigkeit_DSE_temp)
Load distinct 
	%Kundennummer_Standort,
    
   'Postalisch erreichbar' as Ausprägung_Vollständigkeit
    
resident Customer where Customer.Flag_Adresse_vollständig = 1;


Concatenate(Vollständigkeit_DSE_temp)
Load distinct 
	%Kundennummer_Standort,
    
    'Email erreichbar' as Ausprägung_Vollständigkeit
    
resident Customer where [Customer.Flag_Email_vollständig] = 1 or [Customer.Flag_HomeEmail_vollständig] = 1;

Concatenate(Vollständigkeit_DSE_temp)
Load distinct 
	%Kundennummer_Standort,
    
    'Telefon erreichbar' as Ausprägung_Vollständigkeit
    
resident Customer where (Customer.Flag_Phone_vollständig = 1) or Customer.Flag_HomePhone_vollständig = 1 or Customer.Flag_MobilePhone_vollständig = 1  ;



Qualify*;
Unqualify '%*';


NoConcatenate
Vollständigkeit_DSE:
Load
*
resident Vollständigkeit_DSE_temp;

drop table Vollständigkeit_DSE_temp;


/*
------------------------------------------------------------Fehler F1------------------------------------------------------------
*/
Unqualify*;

Fehler_F1:
Load distinct 
	%Kundennummer_Standort,
    
    'E-Mail falsch' as Fehler_F1.Fehler,
     1 as Fehler_F1.Stufe,
     'Fehler' as Fehler_F1.Art
    
resident Customer where (Customer.Flag_Email_vollständig = 0 and Customer.Flag_EMail_empty = 0) or(Customer.Flag_HomeEmail_vollständig = 0 and Customer.Flag_HomeEMail_empty = 0);


Concatenate(Fehler_F1)
Load distinct 
	%Kundennummer_Standort,
    
     'Telefon falsch' as Fehler_F1.Fehler,
     1 as Fehler_F1.Stufe,
     'Fehler' as Fehler_F1.Art
    
resident Customer where ([Customer.Flag_HomePhone_vollständig] = 0 and Customer.Flag_HomePhoneNo_empty = 0) or ([Customer.Flag_MobilePhone_vollständig] = 0 and Customer.Flag_MobilePhoneNo_empty = 0) or ([Customer.Flag_Phone_vollständig] =0 and Customer.Flag_PhoneNo_empty = 0);


Concatenate(Fehler_F1)
Load distinct 
	%Kundennummer_Standort,
    
     'Adresse falsch' as Fehler_F1.Fehler,
     1 as Fehler_F1.Stufe,
     'Fehler' as Fehler_F1.Art
    
resident Customer where [Customer.Flag_Adresse_vollständig] = 0;



Concatenate(Fehler_F1)
Load distinct 
	%Kundennummer_Standort,
    'Keine Kontaktdaten' as Fehler_F1.Fehler,
     1 as Fehler_F1.Stufe,
     'Fehler' as Fehler_F1.Art
resident Customer where 
Customer.Flag_EMail_empty = 1 and 
Customer.Flag_HomeEMail_empty = 1 and
Customer.Flag_HomePhoneNo_empty = 1 and
Customer.Flag_MobilePhoneNo_empty = 1 and
Customer.Flag_PhoneNo_empty = 1 and 
(
Customer.Flag_Address_empty = 1 or 
Customer.Flag_City_empty = 1 or 
Customer.Flag_PostCode_empty = 1
)
;




//DSE Fehler

NoConcatenate    
DSE_temp:
Load distinct 
		%Kundennummer_Standort,
        %Kundennummer
resident Customer;

Left join (DSE_temp)
Load distinct 
  	 %Kundennummer,
    '1' as Flag_DSE_EMail
Resident Data_Protection where [Data_Protection.Contact Email Dealership] = 1;

Left join (DSE_temp)
Load distinct 
   %Kundennummer,
    '1' as Flag_DSE_Post
Resident Data_Protection where [Data_Protection.Contact written Dealership] = 1;

Left join (DSE_temp)
Load distinct 
    %Kundennummer,
    '1' as Flag_DSE_Tel
Resident Data_Protection where [Data_Protection.Contact phone Dealership] = 1;




Left join (DSE_temp)
Load distinct 
	%Kundennummer_Standort,
     '1' as Flag_Email_falsch
resident Customer where Customer.Flag_Email_vollständig = 0 and Customer.Flag_HomeEmail_vollständig = 0;

Left join (DSE_temp)
Load distinct 
	%Kundennummer_Standort,
    '1' as Flag_Phone_falsch
resident Customer where [Customer.Flag_HomePhone_vollständig] = 0 and [Customer.Flag_MobilePhone_vollständig] = 0 and [Customer.Flag_Phone_vollständig] =0;

Left join (DSE_temp)
Load distinct 
	%Kundennummer_Standort,
    '1' as Flag_Adresse_falsch
resident Customer where [Customer.Flag_Adresse_vollständig] = 0;


//DSE ohne befüllung Kanal
Concatenate(Fehler_F1)
Load distinct 
		%Kundennummer_Standort,
    'DSE E-Mail mit Fehler' as Fehler_F1.Fehler,
     2 as Fehler_F1.Stufe,
     'Fehler' as Fehler_F1.Art
resident DSE_temp where Flag_DSE_EMail = '1' and Flag_Email_falsch = '1';

Concatenate(Fehler_F1)
Load distinct 
	[%Kundennummer_Standort],
    'DSE Post mit Fehler' as Fehler_F1.Fehler,
     2 as Fehler_F1.Stufe,
     'Fehler' as Fehler_F1.Art
resident DSE_temp where Flag_DSE_Post = 1 and Flag_Adresse_falsch = '1';

Concatenate(Fehler_F1)
Load distinct 
	[%Kundennummer_Standort],
    'DSE Telefon mit Fehler' as Fehler_F1.Fehler,
     2 as Fehler_F1.Stufe,
     'Fehler' as Fehler_F1.Art
resident DSE_temp where Flag_DSE_Tel = '1' and Flag_Phone_falsch = '1';

// Qualify *;
// Unqualify '%*';

drop table DSE_temp;

NoConcatenate    
DSE_temp_vorlage:
Load distinct 
		%Kundennummer_Standort,
        %Kundennummer
resident Customer;

Left join (DSE_temp_vorlage)
Load distinct 
    %Kundennummer,
    '1' as Flag_vorlage_dse
Resident Data_Protection where not isNUll("Data_Protection.Datum Erkl.Händler");


Concatenate(Fehler_F1)
Load distinct 
	%Kundennummer_Standort,
    'Keine DSE' as Fehler_F1.Fehler,
     2 as Fehler_F1.Stufe,
     'Unvollst.' as Fehler_F1.Art
resident DSE_temp_vorlage where isNull(Flag_vorlage_dse);

drop table DSE_temp_vorlage;


   Qualify *;
Unqualify '%*';

drop field [%Kundennummer] from Fehler_F1;
    
/*
------------------------------------------------------------Kal------------------------------------------------------------
*/
Unqualify *;

//*************** Kalender ***************
LET v_Starttag =  left(num(date(YearStart(AddYears(Today(), -5)))), 5);
//LET v_Endetag = left(num(date('09.11.2021')), 5);//left(num(date(YearEnd(today()))), 5);
LET v_Endetag = left(num(date(today())), 5);//left(num(date(YearEnd(today()))), 5);
LET v_AnzTage = $(v_Endetag) - $(v_Starttag);

Let v_Heute= num(today());

KalenderTMP:
LOAD
	$(v_Starttag) + RowNo() - 1 as %DatumNum
AUTOGENERATE($(v_AnzTage));


// GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE GEDE 


Kalender_NAV_temp:
LOAD
	*,
	'KW ' & week(%DatumNum) & '/' & weekyear(%DatumNum) as EindeutigeWoche,
	weekyear(%DatumNum) * 100 +  week(%DatumNum)		as EindeutigeWocheNum,
	Jahr & '-' & Monat									as EindeutigerMonat,
	Jahr * 100 + Monat									as EindeutigerMonatNum,
// 	Jahr & '-' & Quartal								as EindeutigesQuartal,
	
	%DatumNum - $(v_Heute)											as TageVonHeute,
	(weekstart(%DatumNum) - weekstart($(v_Heute))) / 7				as WochenVonHeute,
	(Jahr - year($(v_Heute))) * 12 + (Monat - month($(v_Heute))) 	as MonateVonHeute,
	Jahr - year($(v_Heute))											as JahreVonHeute,
	
	KWJahr - weekyear($(v_Heute))									as KWJahreVonHeute,
	-networkdays(%DatumNum, $(v_Heute))	+ 1				as ArbeitstageVonHeute,
	
	InYearToDate(%DatumNum, $(v_Heute), 0) * -1			as Flag_YTDAktuellesJahr,
	InYearToDate(%DatumNum, $(v_Heute), -1) * -1		as Flag_YTDVorigesJahr,
	
	1													as Anz_Tage
	;
LOAD
	%DatumNum,
    %DatumNum												as %DatumNum_NAV,
	date(%DatumNum)										as Datum,
	weekday(%DatumNum) & ' ' & date(%DatumNum, 'DD.MM.')as WochentagDatum,
	weekday(%DatumNum)									as Wochentag,
	month(%DatumNum)									as Monat,
// 	applymap('Map_Quartale', month(%DatumNum), '---') 	as Quartal,
	year(%DatumNum)										as Jahr,
	week(%DatumNum)										as Woche,
	weekyear(%DatumNum)									as KWJahr
RESIDENT KalenderTMP; 

drop Field [%DatumNum] from Kalender_NAV_temp;

 drop table KalenderTMP;
 
 Qualify*;
Unqualify '%*';


 NoConcatenate
Kalender_NAV:
Load
	*
Resident Kalender_NAV_temp;

drop table Kalender_NAV_temp;


/*
------------------------------- Store QVD -----------------------------------------
*/
Store * from Telefon_Fehler into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Telefon_Fehler.qvd] (qvd);
//drop table Telefon_Fehler;

Store * from Fehler_F1 into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Fehler_F1.qvd] (qvd);
//drop table Fehler_F1;

Store * from Mail_Fehler into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Mail_Fehler.qvd] (qvd);
//drop table Mail_Fehler;

Store * from Standorte into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Standorte.qvd] (qvd);
//drop table Standorte;

Store * from Data_Protection into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Data_Protection.qvd] (qvd);
//drop table Data_Protection;

Store * from Customer into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Customer.qvd] (qvd);
//drop table Customer;

Store * from Kundenart_F1 into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Kundenart_F1.qvd] (qvd);
//drop table Kundenart_F1;

Store * from Kundentyp into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Kundentyp.qvd] (qvd);
//drop table Kundentyp;

Store * from Kalender_NAV into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Kalender_NAV.qvd] (qvd);
//drop table Kalender_NAV;

Store * from Vollständigkeit_DSE into [lib://$(v_bereich):DataFiles/$(v_start_datei)_03_F1_Vollständigkeit_DSE.qvd] (qvd);
// drop table Vollständigkeit_DSE;
